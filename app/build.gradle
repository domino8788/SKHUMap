apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

group='com.github.kizitonwose'
def artifact = 'CalendarView'

// local.properties 파일에서 API 키 읽기
def localPropertiesFile = rootProject.file('local.properties')
def localProperties = new Properties()
if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

// API 키 가져오기: 1) 환경변수 2) local.properties 3) gradle.properties 순서로 확인
def getApiKey = { ->
    return System.getenv('NAVER_MAPS_API_KEY') ?: 
           localProperties.getProperty('NAVER_MAPS_API_KEY') ?: 
           project.findProperty('NAVER_MAPS_API_KEY') ?: 
           ''
}

android {
    namespace "com.domino.skhumap"
    compileSdkVersion 34

    configurations.all {
        resolutionStrategy {
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        }
    }

    defaultConfig {
        applicationId "com.domino.skhumap"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.3.3"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        // 네이버 지도 API 키를 manifest에 전달
        def apiKey = getApiKey()
        if (apiKey.isEmpty()) {
            throw new GradleException("네이버 지도 API 키가 설정되지 않았습니다. 다음 중 하나의 방법으로 설정하세요:\n" +
                    "1. 환경변수: NAVER_MAPS_API_KEY=your_api_key\n" +
                    "2. local.properties 파일에: NAVER_MAPS_API_KEY=your_api_key\n" +
                    "3. gradle.properties 파일에: NAVER_MAPS_API_KEY=your_api_key")
        }
        
        manifestPlaceholders = [
            NAVER_MAPS_API_KEY: apiKey
        ]
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += ['-module-name', "$group.$artifact"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.fragment:fragment:1.2.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.naver.maps:map-sdk:3.22.0'
    implementation 'com.google.firebase:firebase-analytics:21.0.0'
    implementation 'com.google.firebase:firebase-firestore:24.4.0'
    implementation 'com.google.firebase:firebase-auth:21.3.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.github.arimorty:floatingsearchview:2.1.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.3.7'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.7.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.7.2'
    implementation 'com.github.kizitonwose:CalendarView:0.3.4'
    // Recommended: Add the Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics-ktx:21.0.0'
    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics:18.2.13'
}
